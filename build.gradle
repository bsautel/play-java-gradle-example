plugins {
    id 'play'
    id 'idea'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

repositories {
    jcenter()
    maven {
        name "typesafe-maven-release"
        url "https://repo.typesafe.com/typesafe/maven-releases"
    }
    ivy {
        name "typesafe-ivy-release"
        url "https://repo.typesafe.com/typesafe/ivy-releases"
        layout "ivy"
    }
}

model {
    components {
        play {
            platform play: '2.5.16', scala: '2.11', java: '1.8'
            injectedRoutesGenerator = true
            
            // this is required so that not fully qualified class names in the twirl templates look for in the default Java imports, and not in the default Scala ones
            // for instance, this ensures that with List one refers to java.util.List and not scala.collections.immutable.List
            sources {
                twirlTemplates {
                    defaultImports = TwirlImports.JAVA
                }
            }
        }
    }
}

dependencies {
    play project(':hello')
}

subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.6.2'
    }
}

